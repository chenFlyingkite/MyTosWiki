apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

String libraryVersion = "27.1.1"
def googlePlayVersion = "9.8.0"

task MyBuildInfo {
    ext.versionCode = 1
    ext.versionName = "1.0.0"
    ext.APK_NAME = "MyTosWiki-${versionName}.apk"
}

android {
    signingConfigs {
        config {
            keyAlias 'MyAndroidKey'
            keyPassword 'MyAndroid'
            storeFile file('../../KeyStores/chenflyingkite.jks')
            storePassword 'MyAndroid'
        }
    }

    // https://google.github.io/android-gradle-dsl/current/com.android.build.gradle.internal.dsl.DexOptions.html
    dexOptions {
        jumboMode true
        javaMaxHeapSize "1g"
        maxProcessCount 4 // Total Memory = maxProcessCount * javaMaxHeapSize
        threadCount (maxProcessCount * 3)
    }

    lintOptions {
        warning 'MissingTranslation'
        ignore  'ContentDescription'
        ignore  'RtlCompat', 'RtlEnabled', 'RtlHardcoded', 'RtlSymmetry'
        //abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        applicationId "com.flyingkite.mytoswiki"
        minSdkVersion 19
        targetSdkVersion 27
        multiDexEnabled true
        versionCode MyBuildInfo.versionCode
        versionName MyBuildInfo.versionName
    }


    buildTypes {
        debug {
            signingConfig signingConfigs.config
            applicationIdSuffix ".debug"
        }
        release {
            signingConfig signingConfigs.config
            //minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    project.gradle.addListener(new TaskTicTac())
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "com.android.support:multidex:1.0.1"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "com.android.support:appcompat-v7:$libraryVersion"
    implementation "com.android.support:design:$libraryVersion"
    implementation "com.android.support:percent:$libraryVersion"
    implementation "com.android.support:recyclerview-v7:$libraryVersion"
    implementation "com.android.support.constraint:constraint-layout:1.0.2"

    //implementation 'com.github.User:Repo:Tag'
    //implementation "com.github.chenFlyingkite:MyAndroid:0.0.5.0"
    implementation "com.github.chenFlyingkite:MyAndroid:2431683fb8"

    // A Java serialization/deserialization library to convert Java Objects into JSON and back
    //https://github.com/google/gson
    implementation "com.google.code.gson:gson:2.8.2"

    // Glide for Image cache library.
    // https://github.com/bumptech/glide
    implementation "com.github.bumptech.glide:glide:4.7.1"
    annotationProcessor 'com.github.bumptech.glide:compiler:4.7.1'

    // MathView is a third-party view library, which might help you display math formula on Android apps easier.
    // https://github.com/kexanie/MathView
    implementation 'io.github.kexanie.library:MathView:0.0.6'

    // Drawing the Charts like Excel in Android
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.3'


    // OkHttp for web
    // http://square.github.io/okhttp/
    //implementation "com.squareup.okhttp3:okhttp:3.9.1"

    // jsoup HTML parser library @ https://jsoup.org/
    // https://github.com/jhy/jsoup
    //implementation "org.jsoup:jsoup:1.11.2"
    //
    // https://github.com/Wikia/vignette#scale-to-width
    

    // Picasso
    //implementation 'com.squareup.picasso:picasso:2.5.2'
}

android.applicationVariants.all { variant ->
    def buildType = variant.buildType.name

    println "[applicationVariants.all] ${variant.name}"
    if (buildType == "release") {
        variant.assemble.doLast {
            variant.outputs.each { output ->
                println "[assemble.doLast] copy" +
                    "\n    from(${output.outputFile})" +
                    "\n    into(${rootDir}/apks-${buildType})" +
                    "\n    rename(${MyBuildInfo.APK_NAME})"
                copy {
                    from output.outputFile
                    into "${rootDir}/apks-${buildType}"
                    rename '(.*)', MyBuildInfo.APK_NAME
                }
            }
//            if (variant.getBuildType().isMinifyEnabled()) {
//                println "[assemble.doLast] copy from(${variant.mappingFile}) into(${buildType}) rename(${makeInfo.MAPPING})"
//                copy {
//                    from variant.mappingFile
//                    into "${rootDir}/${buildType}"
//                    rename '(.*)', makeInfo.MAPPING
//                }
//            }
        }
    }
}


// BuildAdapter is simple listener for BuildListener
// TaskExecutionAdapter is simple listener for TaskExecutionListener
class TaskTicTac extends BuildAdapter implements TaskExecutionListener {
    private TicTac clock = new TicTac()
    private times = []
    private everSlow = false
    private final SLOW = 3000

    @Override
    void beforeExecute(Task task) {
        clock.tic()
    }

    @Override
    void afterExecute(Task task, TaskState taskState) {
        long ms = clock.tacL()
        if (!everSlow) {
            everSlow = ms >= SLOW
        }
        times.add([ms, task.path])
    }

    @Override
    void buildFinished(BuildResult result) {
        if (!everSlow) return

        printf "---- TaskTicTac ---- (All %s tasks)\n", times.size()
        printf "Tasks spend time >= %s ms:\n", SLOW
        for (time in times) {
            if (time[0] >= SLOW) {
                printf "%10.03f s  %s\n", time[0] * 0.001, time[1]
            }
        }
        println "--------"
    }
}

class TicTac {
    private Stack<Long> tictac = new Stack<>()

    void reset() {
        tictac.clear()
    }

    long tic() {
        long tic = System.currentTimeMillis()
        tictac.push(tic)
        return tic
    }

    long tacL() {
        long tac = System.currentTimeMillis()
        if (tictac.size() < 1) {
            logError(tac, "")
            return -1
        } else {
            long tic = tictac.pop()
            return tac - tic
        }
    }

    void tac() {
        long tac = System.currentTimeMillis()

        if (tictac.size() < 1) {
            logError(tac, msg)
        } else {
            long tic = tictac.pop()
            StringBuilder s = new StringBuilder()
            int n = tictac.size()

            for(int i = 0; i < n; ++i) {
                s.append(" ")
            }

            s.append("[").append(tac - tic).append("] : ").append(msg)
            logTac(s.toString())
        }
    }

    void logTac(String s) {
        printf "Hi, $s\n"
    }

    void logError(long tac, String msg) {
        printf "Hi, X_X [tic = N/A, tac = $tac] : $msg\n"
    }
}
