// Beginners Tutorial
// http://grails.asia/groovy-map-tutorial
// http://grails.asia/groovy-each-examples

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
// Firebase : Add to the bottom of the file
apply plugin: 'com.google.gms.google-services'
// Add the Firebase Crashlytics plugin.
apply plugin: 'com.google.firebase.crashlytics'

boolean createPlayApk = true

task MyBuildInfo {
    ext.versionCode = 99
    ext.versionName = "1.0.0.${versionCode-1}"
    if (!createPlayApk) {
        ext.versionName = "Me"
    }
    ext.APK_NAME = "MyTosWiki-${versionName}-${versionCode}.apk"
    ext.MAPPING = "MyTosWiki-${versionName}-${versionCode}-mapping.txt"
}

android {
    signingConfigs {
        config {
            keyAlias 'MyAndroidKey'
            keyPassword 'MyAndroid'
            storeFile file('../../KeyStores/chenflyingkite.jks')
            storePassword 'MyAndroid'
        }
    }

    // https://google.github.io/android-gradle-dsl/current/com.android.build.gradle.internal.dsl.DexOptions.html
    dexOptions {
        jumboMode false
        javaMaxHeapSize "1g"
        maxProcessCount 4 // Total Memory = maxProcessCount * javaMaxHeapSize
        threadCount (maxProcessCount * 3)
    }

    // See this for options
    // http://tools.android.com/tips/lint-checks
    lintOptions {
        warning 'MissingTranslation'
        ignore  'ContentDescription', 'SpUsage', 'ExtraTranslation'
        ignore  'RtlCompat', 'RtlEnabled', 'RtlHardcoded', 'RtlSymmetry'
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    // https://kotlinlang.org/docs/reference/using-gradle.html
    kotlinOptions {
        jvmTarget = "1.8"
    }

    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    flavorDimensions "default"

    defaultConfig {
        applicationId "com.flyingkite.mytoswiki"
        minSdkVersion 16
        targetSdkVersion 29
        multiDexEnabled true
        versionCode MyBuildInfo.versionCode
        versionName MyBuildInfo.versionName

        // https://stackoverflow.com/questions/44322178/room-schema-export-directory-is-not-provided-to-the-annotation-processor-so-we
        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas")
            }
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.config
            applicationIdSuffix ".debug"


            // --- Crashlytics --- start

            firebaseCrashlytics {
                // If you don't need crash reporting for your debug build,
                // you can speed up your build by disabling mapping file uploading.
                mappingFileUploadEnabled false
            }

            manifestPlaceholders.crashlyticsEnabled = false
            // https://developer.android.com/studio/build/optimize-your-build.html#disable_crashlytics
            // https://stackoverflow.com/a/49528129
            ext.enableCrashlytics = false
            // https://docs.fabric.io/android/crashlytics/build-tools.html
            // Only use this flag on builds you don't proguard or upload to beta-by-crashlytics
            ext.alwaysUpdateBuildId = false
            // --- Crashlytics --- end
        }
        release {
            signingConfig signingConfigs.config
            minifyEnabled true
            zipAlignEnabled true
            //jniDebuggable false
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            // --- Crashlytics --- start
            manifestPlaceholders.crashlyticsEnabled = true
            ext.enableCrashlytics = true
            // --- Crashlytics --- end
        }
    }

    project.gradle.addListener(new TaskTicTac())
}

// Implementation Vs Api in Android Gradle plugin 3.0
// https://medium.com/mindorks/implementation-vs-api-in-gradle-3-0-494c817a6fa
// https://developer.android.com/studio/build/gradle-plugin-3-0-0-migration#variant_aware
dependencies {
    api fileTree(include: ['*.jar'], dir: 'libs')
    api "androidx.multidex:multidex:2.0.1"
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    api "androidx.appcompat:appcompat:1.2.0"
    api "com.google.android.material:material:1.2.1"
    api "androidx.percentlayout:percentlayout:1.0.0"
    api "androidx.recyclerview:recyclerview:1.1.0"
    api 'androidx.constraintlayout:constraintlayout:2.0.1'
    api "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"

    // Firebase
    // https://firebase.google.com/docs/android/setup
    // https://console.firebase.google.com/project/mytoswiki/overview
    api 'com.google.firebase:firebase-core:17.5.0'
    api 'com.google.firebase:firebase-config:19.2.0'
    api 'com.google.firebase:firebase-messaging:20.2.4'
    // Recommended: Add the Google Analytics SDK.
    api 'com.google.firebase:firebase-analytics:17.5.0'
    // Add the Firebase Crashlytics SDK.
    api 'com.google.firebase:firebase-crashlytics:17.2.1'
    // Add the Firebase Crashlytics NDK dependency.
    //api 'com.google.firebase:firebase-crashlytics-ndk:17.2.1'

    // Flurry Analytics
    // https://developer.yahoo.com/flurry/docs/integrateflurry/android/

    // See usage in https://jitpack.io/
    //api 'com.github.User:Repo:Tag'
    String ver = ''
    ver = 'a841193247'
    ver = '4df6b34674'
    api "com.github.chenFlyingkite:MyAndroid:$ver"
    //api "com.github.chenFlyingkite:MyAndroid:1f5c017f30"

    // A Java serialization/deserialization library to convert Java Objects into JSON and back

    // https://github.com/google/gson
    api 'com.google.code.gson:gson:2.8.6'

    // jsoup HTML parser library @ https://jsoup.org/
    // https://github.com/jhy/jsoup
    api 'org.jsoup:jsoup:1.11.3'

    // Room Persistence Library
    // https://developer.android.com/training/data-storage/room/
//    def roomVersion = "1.1.1"
//    api "android.arch.persistence.room:runtime:$roomVersion"
//    kapt "android.arch.persistence.room:compiler:$roomVersion"

    // ViewModel for data passing through fragment & activity, Not using v4 fragment...
    //api "android.arch.lifecycle:viewmodel:1.1.1"
    //api "android.arch.lifecycle:extensions:1.1.1"


    // Glide for Image cache library.
    // https://github.com/bumptech/glide
    //api "com.github.bumptech.glide:glide:3.8.0" // fails for 4.4
    api "com.github.bumptech.glide:glide:4.11.0"
    kapt 'com.github.bumptech.glide:compiler:4.11.0'
    //annotationProcessor 'com.github.bumptech.glide:compiler:4.7.1'

    // MathView is a third-party view library, which might help you display math formula on Android apps easier.
    // https://github.com/kexanie/MathView
    //api 'io.github.kexanie.library:MathView:0.0.6'

    // Drawing the Charts like Excel in Android
    api 'com.github.PhilJay:MPAndroidChart:v3.0.3'

    // OkHttp for web
    // https://github.com/square/okhttp/
    // http://square.github.io/okhttp/
    api "com.squareup.okhttp3:okhttp:4.6.0"

    // https://github.com/Wikia/vignette#scale-to-width
    // Picasso
    //api 'com.squareup.picasso:picasso:2.5.2'
    //api 'com.squareup.picasso:picasso:2.71828'
}

android.applicationVariants.all { variant ->
    def buildType = variant.buildType.name

    if (buildType == "release") {
        assembleRelease.doLast {
            copyAPK(variant)
        }

        bundleRelease.doLast {
            copyAPK(variant)
            copyAAB(variant)
        }
    }
}

//--
def copyAPK(variant) {
    // variant : ApplicationVariant
    // https://android.googlesource.com/platform/tools/build/+/tools_r22/gradle/src/main/groovy/com/android/build/gradle/internal/api
    description 'copy *.apk and mapping file'
    variant.outputs.each { output ->
        Using u = new Using(project)
        String type = variant.name // = "debug" or "release"
        // Move APK, E.g.
        // D:\ActionDirector_Android\ACD_01\ActionDirector\build\outputs\apk\release\ActionDirector-release.apk
        u.copy(
            "${output.outputFile}",
            "${rootDir}/apks-${type}",
            "${MyBuildInfo.APK_NAME}"
        )
        if (variant.getBuildType().isMinifyEnabled()) {
            // Move mapping, E.g.
            // D:\ActionDirector_Android\ACD_01\ActionDirector\build\outputs\mapping\release\mapping.txt
            u.copy(
                "${variant.mappingFile}",
                "${rootDir}/apks-${type}",
                "${MyBuildInfo.MAPPING}"
            )
            // Extra no need file...
            String s = "${rootDir}/app/mapping.txt"
            delete(s)
            println("delete $s")
        }
    }
}

def copyAAB(variant) {
    String type = variant.name // = "debug" or "release"
    // Move AAB, E.g.
    // D:\ActionDirector_Android\ACD_01\ActionDirector\build\outputs\bundle\release\ActionDirector.aab
    Using u = new Using(project)
    u.copy(
        "$buildDir/outputs/bundle/$type/${project.name}.aab",
        "${rootDir}/${type}",
        "${makeInfo.AAB}"
    )
}

class Using {
    private Project project

    Using(Project p) {
        project = p
    }

    Using copy(String _from, String _into, String _rename) {
        // Object is type of org.codehaus.groovy.runtime.GStringImpl, here we take String
        println "copy from $_from\n >   into $_into\n > rename $_rename\n"
        project.copy {
            from _from
            into _into
            rename '(.*)', _rename
        }
        return this
    }
}
//--


// BuildAdapter is simple listener for BuildListener
// TaskExecutionAdapter is simple listener for TaskExecutionListener
class TaskTicTac extends BuildAdapter implements TaskExecutionListener {
    private TicTac clock = new TicTac()
    private times = []
    private everSlow = false
    private final SLOW = 3000

    @Override
    void beforeExecute(Task task) {
        clock.tic()
    }

    @Override
    void afterExecute(Task task, TaskState taskState) {
        long ms = clock.tacL()
        if (!everSlow) {
            everSlow = ms >= SLOW
        }
        times.add([ms, task.path])
    }

    @Override
    void buildFinished(BuildResult result) {
        if (!everSlow) return

        printf "---- TaskTicTac ---- (All %s tasks)\n", times.size()
        printf "Tasks spend time >= %s ms:\n", SLOW
        for (time in times) {
            if (time[0] >= SLOW) {
                printf "%10.03f s  %s\n", time[0] * 0.001, time[1]
            }
        }
        println "--------"
    }
}

class TicTac {
    private Stack<Long> tictac = new Stack<>()

    void reset() {
        tictac.clear()
    }

    long tic() {
        long tic = System.currentTimeMillis()
        tictac.push(tic)
        return tic
    }

    long tacL() {
        long tac = System.currentTimeMillis()
        if (tictac.size() < 1) {
            logError(tac, "")
            return -1
        } else {
            long tic = tictac.pop()
            return tac - tic
        }
    }

    void tac() {
        long tac = System.currentTimeMillis()

        if (tictac.size() < 1) {
            logError(tac, msg)
        } else {
            long tic = tictac.pop()
            StringBuilder s = new StringBuilder()
            int n = tictac.size()

            for (int i = 0; i < n; ++i) {
                s.append(" ")
            }

            s.append("[").append(tac - tic).append("] : ").append(msg)
            logTac(s.toString())
        }
    }

    void logTac(String s) {
        printf "Hi, $s\n"
    }

    void logError(long tac, String msg) {
        printf "Hi, X_X [tic = N/A, tac = $tac] : $msg\n"
    }
}
